buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

repositories {
	maven {
		name = "CB Repo"
		url = "http://chickenbones.net/maven"
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

group = "codechicken" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ForcedDeobfuscator"

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
	// Load config.  It shall from now be referenced as simply config or project.config
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

dependencies {
}

version = "${project.config.mod_version}." + (System.getenv("BUILD_NUMBER") ?: "1")

println config.mc_version + "-" + config.forge_version
// Setup the forge minecraft plugin data. Specify the preferred forge/minecraft version here
minecraft {
	version = config.mc_version + "-" + config.forge_version
	mappings = config.mappings
	runDir = "run"
	replace '${mod_version}', project.config.mod_version
	makeObfSourceJar = false
}


processResources {
	inputs.property "version", project.version
	inputs.property "mc_version", project.config.mc_version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mc_version': project.config.mc_version
	}
	rename '(.+_at.cfg)', 'META-INF/$1'
}

version = "${project.minecraft.version}-${project.version}"

task srcJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf { // Skip the task if our secret data isn't available
		project.hasProperty('keyStore')
	}

	if (project.hasProperty('keyStore')) {
		keyStore = project.keyStore // This needs to be a path to the keystore file
		alias = project.keyStoreAlias
		storePass = project.keyStorePass
		keyPass = project.keyStoreKeyPass
		inputFile = jar.archivePath
		outputFile = jar.archivePath
	}
}

build.dependsOn signJar

// Tell the artifact system about our extra jars
artifacts {
	archives srcJar
}

// Configure an upload task.
uploadArchives {
	repositories {
		mavenDeployer {
			if (project.hasProperty("local_maven")) {
				repository(url: "file://${local_maven}")
				pom {
					groupId = project.group
					// Force the maven upload to use the <mcversion>-<version> syntax preferred at files
					artifactId = project.archivesBaseName
					project {
						name project.archivesBaseName
						packaging 'jar'
						description 'ForcedDeobfuscator'
						url 'https://github.com/covers1624/ForcedDeobfuscator'

						scm {
							url 'https://github.com/covers1624/ForcedDeobfuscator'
							connection 'scm:git:git://github.com/covers1624/ForcedDeobfuscator.git'
							developerConnection 'scm:git:git@github.com:covers1624/ForcedDeobfuscator.git'
						}

						issueManagement {
							system 'github'
							url 'https://github.com/covers1624/ForcedDeobfuscator/issues'
						}

						licenses {
							license {
								name 'All Rights Reserved.'
								distribution 'repo'
							}
						}

						developers {
							developer {
								id 'covers1624'
								name 'covers1624'
								roles { role 'developer' }
							}
						}
					}
				}
			}
		}
	}
}
